         MACRO
&NAME    #MEMCPY  &D,&S,&L
         LCLC     &MC
&MC      SETC     '$MC&SYSNDX'
.*
.* Emit a label if one is specified
.*
         AIF      ('&NAME' EQ '').NNAME
&NAME    DS       0H
.NNAME   ANOP
.*
.* Process the destination parm
.*
         AIF      ('&D'(1,1) EQ '(').D1
         AIF      ('&D'(1,1) EQ '*').D2
         LA       R15,&D                    dst = d
         AGO      .D3
.D1      ANOP
         LR       R15,&D                    dst = (R?)
         AGO      .D3
.D2      ANOP
&T       SETC     '&D'(2,*)
         L        R15,&T                    dst = *d
.D3      ANOP
.*
.* Process the source parm
.*
         AIF      ('&S'(1,1) EQ '(').S1
         AIF      ('&S'(1,1) EQ '*').S2
         LA       R14,&S                    src = s
         AGO      .S3
.S1      ANOP
         LR       R14,&S                    src = (R?)
         AGO      .S3
.S2      ANOP
&T       SETC     '&S'(2,*)
         L        R14,&T                    src = *s
.S3      ANOP
.*
.* Process the length parm
.*
         AIF      ('&L'(1,1) EQ '(').L1
         AIF      ('&L'(1,1) EQ '*').L2
         LA       R1,&L                     len = l
         AGO      .L3
.L1      ANOP
         LR       R1,&L                     len = (R?)
         AGO      .L3
.L2      ANOP
&T       SETC     '&L'(2,*)
         L        R1,&T                     len = *l
.L3      ANOP
.*
.* Start copying
.*
         LTR      R1,R1                     len == 0?
         BZ       &MC.4                     Yes, nothing to do
         ST       R15,16(R13)               Save dest ptr
         BCTR     R1,0                      len--
         LR       R0,R1                     t0 = len
         SRL      R0,8                      len /= 256
         LTR      R0,R0                     t0 = 0?
         BZ       &MC.3                     Yes, go move < 256 bytes
&MC.1    MVC      0(256,R15),0(R14)         dstÝ¨ = srcÝ¨
         LA       R14,256(R14)              src += 256
         LA       R15,256(R15)              dst += 256
         BCT      R0,&MC.1                  (t0--) == 0?continue:break
&MC.2    MVC      0(0,R15),0(R14)           Set 256 bytes
&MC.3    EX       R1,&MC.2                  Move < 256 bytes
         L        R15,16(R13)               Restore dest ptr
&MC.4    DS       0H
         MEND
