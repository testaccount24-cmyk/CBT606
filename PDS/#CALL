         MACRO
&NAME    #CALL    &MF=I
         COPY     #GLOBAL
         GBLC     &$FC
         GBLC     &$FCSD(1)
         GBLA     &$FCSC
         GBLA     &$FCPC
         LCLA     &I
         LCLA     &PI
         LCLA     &STM1
         LCLA     &STM2
         LCLC     &TC
         LCLA     &TA
.*
&NAME    DS       0H
.*---------------------------------------------------------------------
.* Intialize globals if we haven't been through here yet
.*---------------------------------------------------------------------
         AIF      ('&$FC' NE '').L1
&$FC     SETC     'SP'.'&SYSNDX'(2,*)
&$FCPC   SETA     1
&$FCSC   SETA     0
.L1      ANOP
.*---------------------------------------------------------------------
.* Jump to the appropriate section
.*---------------------------------------------------------------------
         AIF      ('&MF' EQ 'I').MFI
         AIF      ('&MF' EQ 'L').MFL
         MNOTE    8,'MF= must be (I)nline or (L)ist'
         MEXIT
.*---------------------------------------------------------------------
.* Process parameters, call snprintf, and put record to SYSPRINT
.*---------------------------------------------------------------------
.MFI     ANOP
.**********************************************************************
.* START OF LOOP                                                      *
.**********************************************************************
.*
&I       SETA     2
&PI      SETA     0
.LOOPS   AIF      (&I GT N'&SYSLIST).LOOPE
.*
&TC      SETC     T'&SYSLIST(&I)
         AIF      ('&SYSLIST(&I)' EQ '').L10
         AIF      ('&SYSLIST(&I)'(1,1) EQ '=').L11
         AIF      ('&SYSLIST(&I)'(1,1) EQ '*').L12
         AIF      ('&SYSLIST(&I)'(1,1) EQ '''').L13
         AIF      ('&SYSLIST(&I)'(1,1) EQ '(').L14
         AIF      (T'&SYSLIST(&I) EQ 'N').L15
.*---------------------------------------------------------------------
.* Default is address specification
.*---------------------------------------------------------------------
         LA       R14,&SYSLIST(&I)
         ST       R14,&L.$PL+&PI
         AGO      .L21
.*---------------------------------------------------------------------
.* Null specification - use previous value
.*---------------------------------------------------------------------
.L10     ANOP
         AGO      .L21
.*---------------------------------------------------------------------
.* Literal
.*---------------------------------------------------------------------
.L11     ANOP
         MVC      &L.$PL+&PI.(4),&SYSLIST(&I)
         AGO      .L21
.*---------------------------------------------------------------------
.* Indirect specification
.*---------------------------------------------------------------------
.L12     ANOP
&P       SETC     '&SYSLIST(&I)'(2,*)
         MVC      &L.$PL+&PI.(4),&P
         AGO      .L21
.*---------------------------------------------------------------------
.* String
.*---------------------------------------------------------------------
.L13     ANOP
&$FCSC   SETA     &$FCSC+1
&$FCSD(&$FCSC)   SETC     '&SYSLIST(&I)'
         LA       R14,&L.$SD&$FCSC
         ST       R14,&L.$PL+&PI
         AGO      .L21
.*---------------------------------------------------------------------
.* Register notation
.*---------------------------------------------------------------------
.L14     ANOP
.*
&TA      SETA     1
         AIF      (T'&SYSLIST(&I,1) EQ 'N').R1NUM
&TA      SETA     2
.R1NUM   ANOP
&TC      SETC     '&SYSLIST(&I,1)'(&TA,*)
&STM1    SETA     &TC
.*
         AIF      (N'&SYSLIST(&I) GT 1).L14A
         ST       &SYSLIST(&I,1),&L.$PL+&PI
         AGO      .L21
.*---------------------------------------------------------------------
.* Multiple register notation
.*---------------------------------------------------------------------
.L14A    ANOP
&TA      SETA     1
         AIF      (T'&SYSLIST(&I,2) EQ 'N').R1NUM
&TA      SETA     2
.R2NUM   ANOP
&TC      SETC     '&SYSLIST(&I,2)'(&TA,*)
&STM2    SETA     &TC
.*
         AIF      (&STM1 LE &STM2).L14B
&STM2    SETA     &STM2+16
.L14B    ANOP
         STM      &SYSLIST(&I,1),&SYSLIST(&I,2),&L.$PL+&PI
&PI      SETA     &PI+((&STM2-&STM1)*4)
         AGO      .L21
.*---------------------------------------------------------------------
.* Numeric specification
.*---------------------------------------------------------------------
.L15     ANOP
         MVC      &L.$PL+&PI.(4),=F'&SYSLIST(&I)'
         AGO      .L21
.*---------------------------------------------------------------------
.* Bump indexes and continue loop
.*---------------------------------------------------------------------
.L21     ANOP
&PI      SETA     &PI+4
&I       SETA     &I+1
         AGO      .LOOPS
.*---------------------------------------------------------------------
.* Remember highest parameter count
.*---------------------------------------------------------------------
.LOOPE   ANOP
         AIF      (&$FCPC GT &PI).L29
&$FCPC   SETA     &PI
.L29     ANOP
.**********************************************************************
.* END OF LOOP                                                        *
.**********************************************************************
.*
         AIF      (N'&SYSLIST EQ 1).NPL
         LA       R1,&L.$PL
.NPL     ANOP
         AIF      ('&SYSLIST(1)'(1,1) NE '(').NREG
         LR       R15,&SYSLIST(1)
         AGO      .CALL
.NREG    ANOP
         L        R15,=V(&SYSLIST(1))
         AGO      .CALL
.CALL    ANOP
         BALR     R14,R15
         AGO      .MEND
.*
.*
.*
.MFL     ANOP
&L.$SD  DS       0X
.*
.L30     AIF      (&$FCSC EQ 0).L31
&L.$SD&$FCSC #CSTR  &$FCSD(&$FCSC)
&$FCSC   SETA     &$FCSC-1
         AGO      .L30
.L31     ANOP
         AGO      .MEND
.*
.*
.*
.MEND    MEND
