         MACRO
&NAME    #MEMSET  &D,&C,&L
         LCLC     &MS
&MS      SETC     '$MC&SYSNDX'
.*
.* Emit a label if one is specified
.*
         AIF      ('&NAME' EQ '').NNAME
&NAME    DS       0H
.NNAME   ANOP
.*
.* Process the destination parm
.*
         AIF      ('&D'(1,1) EQ '(').D1
         AIF      ('&D'(1,1) EQ '*').D2
         LA       R15,&D                    buf = d
         AGO      .D3
.D1      ANOP
         LR       R15,&D                    buf = (R?)
         AGO      .D3
.D2      ANOP
&T       SETC     '&D'(2,*)
         L        R15,&T                    buf = *d
.D3      ANOP
.*
.* Process the length parm
.*
         AIF      ('&L'(1,1) EQ '(').L1
         AIF      ('&L'(1,1) EQ '*').L2
         LA       R1,&L                     len = l
         AGO      .L3
.L1      ANOP
         LR       R1,&L                     len = (R?)
         AGO      .L3
.L2      ANOP
&T       SETC     '&L'(2,*)
         L        R1,&T                     len = *l
.L3      ANOP
.*
.* Make sure we have anything to set
.*
         LTR      R1,R1                     len == 0?
         BZ       &MS.4                     Yes, nothing to do
.*
.* Process the byte parm
.*
         AIF      ('&C'(1,1) EQ '(').C1
         AIF      ('&C'(1,1) EQ '*').C2
         MVI      0(R15),&C                 bufÝ 0 ¨ = c
         AGO      .C3
.C1      ANOP
         STC      &C,0(R15)                 bufÝ 0 ¨ = (R?)
         AGO      .C3
.C2      ANOP
&T       SETC     '&C'(2,*)
         MVC      0(1,R15),&T               bufÝ 0 ¨ = *c
.C3      ANOP
.*
.* Start clearing
.*
         BCTR     R1,0                      len--
         LTR      R1,R1                     len == 0?
         BZ       &MS.4                     Yes, nothing more to do
         LR       R14,R15                   Save dest ptr
         BCTR     R1,0                      len--
         LR       R0,R1                     t0 = len
         SRL      R0,8                      t0 /= 256
         LTR      R0,R0                     t0 == 0?
         BZ       &MS.3                     Yes, go copy less then 256
&MS.1    MVC      1(256,R15),0(R15)         Set 256 bytes
         LA       R15,256(R15)              buf += 256
         BCT      R0,&MS.1                  (t0--) == 0?continue:break
&MS.2    MVC      1(0,R15),0(R15)           Executed
&MS.3    EX       R1,&MS.2                  Set < 256 bytes
         LR       R15,R14                   Restore dest ptr
&MS.4    DS       0H                        End
         MEND
