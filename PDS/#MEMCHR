         MACRO
&NAME    #MEMCHR  &D,&C,&L
         LCLC     &MC
&MC      SETC     '$MC&SYSNDX'
.*
.* Emit a label if one is specified
.*
         AIF      ('&NAME' EQ '').NNAME
&NAME    DS       0H
.NNAME   ANOP
.*
.* Process the destination parm
.*
         AIF      ('&D'(1,1) EQ '(').D1
         AIF      ('&D'(1,1) EQ '*').D2
         LA       R15,&D                    buf = d
         AGO      .D3
.D1      ANOP
         LR       R15,&D                    buf = (R?)
         AGO      .D3
.D2      ANOP
&T       SETC     '&D'(2,*)
         L        R15,&T                    buf = *d
.D3      ANOP
.*
.* Process the length parm
.*
         AIF      ('&L'(1,1) EQ '(').L1
         AIF      ('&L'(1,1) EQ '*').L2
         LA       R1,&L                     len = l
         AGO      .L3
.L1      ANOP
         LR       R1,&L                     len = (R?)
         AGO      .L3
.L2      ANOP
&T       SETC     '&L'(2,*)
         L        R1,&T                     len = *l
.L3      ANOP
.*
.* Make sure we have something to scan
.*
.*
.* Start clearing
.*
         LTR      R1,R1                     len == 0?
         BZ       &MC.2                     Yes, nothing to do
.*
.* Process the byte parm
.*
         AIF      ('&C'(1,1) EQ '(').C1
         AIF      ('&C'(1,1) EQ '*').C2
&MC.1    CLI      0(R15),&C                 bufÝ 0 ¨ == c?
         AGO      .C3
.C1      ANOP
         MNOTE    8,'Register notation not supported for char parm'
         AGO      .C3
.C2      ANOP
&T       SETC     '&C'(2,*)
&MC.1    CLC      0(1,R15),&T               bufÝ 0 ¨ == *c?
.C3      ANOP
         BE       &MC.2                     Yes, exit loop
         LA       R15,1(R15)                cp++
         BCT      R1,&MC.1                  (--t1) != 0 ? continue loop
         XR       R15,R15                   cp = NULL
&MC.2    DS       0H                        End
         MEND
